USE [DB_PMITG_FGOS]
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE [dbo].[INTEGRACAO_CLIENTES] (@DIRETORIO VARCHAR(200), @NOME_ARQUIVO VARCHAR(200))
AS

BEGIN

DECLARE @SQL		     VARCHAR(MAX)
DECLARE @FIELDTERMINATOR     VARCHAR(10)  
DECLARE @ROWTERMINATOR	     VARCHAR(10)
DECLARE @FIRSTROW	     NUMERIC(15)
DECLARE @TABELA_AFETADA	     VARCHAR(60)
DECLARE @QUANTIDADE_ALTERADO INT
DECLARE @QUANTIDADE_INSERIDO INT
DECLARE @MSG_ERRO	     VARCHAR(MAX)

SET @TABELA_AFETADA = 'CLIENTES'
SET @FIELDTERMINATOR = '	'
SET @ROWTERMINATOR   = '\n'
SET @FIRSTROW        = 2

IF OBJECT_ID('TEMPDB..#TEMP_ARQUIVO_CLIENTES') IS NOT NULL
    DROP TABLE #TEMP_ARQUIVO_CLIENTES

CREATE TABLE #TEMP_ARQUIVO_CLIENTES
 (
	 ID			      VARCHAR(255)
	,EMAIL 			      VARCHAR(255)
	,NOME			      VARCHAR(255)
	,DATA_NASCIMENTO	      VARCHAR(255)
	,SEXO			      VARCHAR(255)
	,DATA_CADASTRO_SISTEMAORIGEM  VARCHAR(255)
	,CIDADE			      VARCHAR(255)
	,UF			      VARCHAR(255)
	,PERMISSAO_RECEBE_EMAIL	      VARCHAR(255)

 )

BEGIN TRY

	BEGIN TRANSACTION

	SET @SQL = 'BULK INSERT #TEMP_ARQUIVO_CLIENTES 
				FROM '''  + @DIRETORIO + '\' + @NOME_ARQUIVO + ''' ' +
			   'WITH (FIELDTERMINATOR = ' + '''' + @FIELDTERMINATOR + '''' +
			   '     ,ROWTERMINATOR   = ' + '''' + @ROWTERMINATOR   + '''' +
			   '     ,FIRSTROW        = ' + CONVERT(VARCHAR,@FIRSTROW) +
			   '	 ,CODEPAGE = ''ACP'' ' +
			   ')'

	EXEC (@SQL)
	
	UPDATE CO
	   SET CO.EMAIL 		      = CD.EMAIL 										
	      ,CO.NOME			      = CD.NOME						
	      ,CO.DATA_NASCIMENTO	      = CD.DATA_NASCIMENTO			
	      ,CO.SEXO			      = CD.SEXO						
	      ,CO.DATA_CADASTRO_SISTEMAORIGEM = CD.DATA_CADASTRO_SISTEMAORIGEM
	      ,CO.CIDADE		      = CD.CIDADE						
	      ,CO.UF			      = CD.UF							
	      ,CO.PERMISSAO_RECEBE_EMAIL      = CD.PERMISSAO_RECEBE_EMAIL
	  FROM CLIENTES CO WITH(NOLOCK)
	  JOIN #TEMP_ARQUIVO_CLIENTES CD ON CD.ID = CO.ID

    SET @QUANTIDADE_ALTERADO = @@ROWCOUNT
	
	INSERT INTO CLIENTES
	(
	       ID							
	      ,EMAIL 						
	      ,NOME						
	      ,DATA_NASCIMENTO			
	      ,SEXO						
	      ,DATA_CADASTRO_SISTEMAORIGEM
	      ,CIDADE						
	      ,UF							
	      ,PERMISSAO_RECEBE_EMAIL	
	)
	SELECT TAC.ID							
	      ,TAC.EMAIL 						
	      ,TAC.NOME						
	      ,TAC.DATA_NASCIMENTO			
	      ,TAC.SEXO						
	      ,TAC.DATA_CADASTRO_SISTEMAORIGEM
	      ,TAC.CIDADE						
	      ,TAC.UF							
	      ,TAC.PERMISSAO_RECEBE_EMAIL	
	 FROM #TEMP_ARQUIVO_CLIENTES TAC
	 LEFT JOIN CLIENTES C WITH(NOLOCK) ON C.ID = TAC.ID
	WHERE dbo.VALIDA_EMAIL(TAC.EMAIL) = 1
	  AND TAC.ID IS NOT NULL
	  AND TAC.NOME IS NOT NULL
	  AND TAC.DATA_NASCIMENTO IS NOT NULL
	  AND TAC.CIDADE IS NOT NULL
	  AND C.ID IS NULL

	SET @QUANTIDADE_INSERIDO = @@ROWCOUNT

	COMMIT

END TRY
BEGIN CATCH

	SET @MSG_ERRO = ERROR_MESSAGE()
	SET @QUANTIDADE_INSERIDO = 0
	SET @QUANTIDADE_ALTERADO = 0

	ROLLBACK

END CATCH

--REGISTRO DO LOG

INSERT INTO LOG_RODADAS
(
    TABELA
   ,QUANTIDADE_ALTERADO
   ,QUANTIDADE_INSERIDO
   ,MENSAGEM_ERRO
)
VALUES
(
    @TABELA_AFETADA
   ,ISNULL(@QUANTIDADE_ALTERADO,0)
   ,ISNULL(@QUANTIDADE_INSERIDO,0)
   ,@MSG_ERRO
)

END



GO
