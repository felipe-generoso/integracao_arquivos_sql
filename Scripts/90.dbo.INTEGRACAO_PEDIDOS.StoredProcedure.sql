USE [DB_PMITG_FGOS]
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE [dbo].[INTEGRACAO_PEDIDOS] (@DIRETORIO VARCHAR(200), @NOME_ARQUIVO VARCHAR(200))
AS

BEGIN

DECLARE @SQL		     VARCHAR(MAX)
DECLARE @FIELDTERMINATOR     VARCHAR(10)  
DECLARE @ROWTERMINATOR	     VARCHAR(10)
DECLARE @FIRSTROW	     NUMERIC(15)
DECLARE @TABELA_AFETADA	     VARCHAR(60)
DECLARE @QUANTIDADE_ALTERADO INT
DECLARE @QUANTIDADE_INSERIDO INT
DECLARE @MSG_ERRO	     VARCHAR(MAX)

SET @TABELA_AFETADA = 'PEDIDOS'
SET @FIELDTERMINATOR = '	'
SET @ROWTERMINATOR   = '\n'
SET @FIRSTROW        = 2

IF OBJECT_ID('TEMPDB..#TEMP_ARQUIVO_PEDIDOS') IS NOT NULL
    DROP TABLE #TEMP_ARQUIVO_PEDIDOS

CREATE TABLE #TEMP_ARQUIVO_PEDIDOS
 (
     ID_CLIENTE		  VARCHAR(255)
    ,ID_PEDIDO		  VARCHAR(255)
    ,ID_PRODUTO		  VARCHAR(255)
    ,DEPARTAMENTO	  VARCHAR(255)
    ,QUANTIDADE		  VARCHAR(255)
    ,VALOR_UNITARIO	  VARCHAR(255)
    ,PARCELAS		  VARCHAR(255)
    ,DATA_PEDIDO	  VARCHAR(255)
    ,MEIO_PAGAMENTO	  VARCHAR(255)
    ,STATUS_PAGAMENTO     VARCHAR(255)
 )

BEGIN TRY

	BEGIN TRANSACTION

	SET @SQL = 'BULK INSERT #TEMP_ARQUIVO_PEDIDOS 
				FROM '''  + @DIRETORIO + '\' + @NOME_ARQUIVO + ''' ' +
			   'WITH (FIELDTERMINATOR = ' + '''' + @FIELDTERMINATOR + '''' +
			   '     ,ROWTERMINATOR   = ' + '''' + @ROWTERMINATOR   + '''' +
			   '     ,FIRSTROW        = ' + CONVERT(VARCHAR,@FIRSTROW) +
			   '	 ,CODEPAGE = ''ACP'' ' +
			   ')'


	EXEC (@SQL)

	UPDATE PO
	   SET PO.DEPARTAMENTO		= PD.DEPARTAMENTO		
	      ,PO.QUANTIDADE		= PD.QUANTIDADE
	      ,PO.VALOR_UNITARIO	= REPLACE(REPLACE(PD.VALOR_UNITARIO,'.',''),',','.')
	      ,PO.PARCELAS		= PD.PARCELAS
	      ,PO.DATA_PEDIDO		= PD.DATA_PEDIDO
	      ,PO.MEIO_PAGAMENTO	= PD.MEIO_PAGAMENTO
	      ,PO.STATUS_PAGAMENTO 	= PD.STATUS_PAGAMENTO
	  FROM PEDIDOS PO WITH(NOLOCK) 
	  JOIN #TEMP_ARQUIVO_PEDIDOS PD ON PD.ID_CLIENTE  = PO.ID_CLIENTE
				       AND PD.ID_PEDIDO   = PO.ID_PEDIDO
				       AND PD.ID_PRODUTO  = PO.ID_PRODUTO

	SET @QUANTIDADE_ALTERADO = @@ROWCOUNT

	INSERT INTO PEDIDOS
	(
	   ID_CLIENTE
	  ,ID_PEDIDO
	  ,ID_PRODUTO
	  ,DEPARTAMENTO
	  ,QUANTIDADE
	  ,VALOR_UNITARIO
	  ,PARCELAS
	  ,DATA_PEDIDO
	  ,MEIO_PAGAMENTO
	  ,STATUS_PAGAMENTO
	) 
    SELECT TAP.ID_CLIENTE		
   	  ,TAP.ID_PEDIDO		
   	  ,TAP.ID_PRODUTO		
   	  ,TAP.DEPARTAMENTO	    
   	  ,TAP.QUANTIDADE		
   	  ,REPLACE(REPLACE(TAP.VALOR_UNITARIO,'.',''),',','.')
   	  ,TAP.PARCELAS		    
   	  ,TAP.DATA_PEDIDO	    
   	  ,TAP.MEIO_PAGAMENTO	
   	  ,TAP.STATUS_PAGAMENTO
     FROM #TEMP_ARQUIVO_PEDIDOS TAP
     LEFT JOIN PEDIDOS P WITH(NOLOCK) ON P.ID_CLIENTE  = TAP.ID_CLIENTE
				     AND P.ID_PEDIDO   = TAP.ID_PEDIDO
				     AND P.ID_PRODUTO  = TAP.ID_PRODUTO
     WHERE P.ID_PEDIDO IS NULL

     SET @QUANTIDADE_INSERIDO = @@ROWCOUNT

     COMMIT

END TRY
BEGIN CATCH

	SET @MSG_ERRO = ERROR_MESSAGE()
	SET @QUANTIDADE_INSERIDO = 0
	SET @QUANTIDADE_ALTERADO = 0

	ROLLBACK

END CATCH

--REGISTRO DO LOG

INSERT INTO LOG_RODADAS
(
    TABELA
   ,QUANTIDADE_ALTERADO
   ,QUANTIDADE_INSERIDO
   ,MENSAGEM_ERRO
)
VALUES
(
    @TABELA_AFETADA
   ,ISNULL(@QUANTIDADE_ALTERADO,0)
   ,ISNULL(@QUANTIDADE_INSERIDO,0)
   ,@MSG_ERRO
)

END



GO
